buildscript {
	dependencies {
		classpath 'com.eriwen:gradle-css-plugin:2.14.0'
		classpath 'com.eriwen:gradle-js-plugin:2.14.1'
	}
}

apply plugin: 'js'
apply plugin: 'css'
apply plugin: 'maven'

configurations {
	reszip {
		canBeConsumed = true
		canBeResolved = false
	}
}

combineCss {
	source = file("${projectDir}/src/css/css.buildorder").readLines().findAll { !it.trim().isEmpty() }
			.collect { "${projectDir}/src/css/${it}" }
	dest = file("${buildDir}/web/debug/css/revolt.css")
}

minifyCss {
	source = combineCss
	dest = "${buildDir}/web/css/revolt.css"
	doLast {
		ant.replaceregexp(match: '(calc\\([\\d|\\.]+[^+]*)(\\+)', replace:'\1 \2 ', flags:'g') {
			file "${buildDir}/web/css/${rootProject.name}.css"
		}
	}
}

task combineFontsCss(type: com.eriwen.gradle.css.tasks.CombineCssTask) {
	source = file("${projectDir}/src/css/fonts.buildorder").readLines().findAll { !it.trim().isEmpty() }
			.collect { "${projectDir}/src/css/${it}" }
	dest = file("${buildDir}/web/debug/css/fonts.css")
}

task minifyFontsCss(type: com.eriwen.gradle.css.tasks.MinifyCssTask) {
	source = combineFontsCss
	dest = "${buildDir}/web/css/fonts.css"
	doLast {
		ant.replaceregexp(match: '(calc\\([\\d|\\.]+[^+]*)(\\+)', replace:'\1 \2 ', flags:'g') {
			file "${buildDir}/web/css/${rootProject.name}.css"
		}
	}
}

combineJs {
	source = file("${projectDir}/src/js/js.buildorder").readLines().findAll { !it.trim().isEmpty() }
			.collect { "${projectDir}/src/js/${it}" }
	dest = file("${buildDir}/web/debug/revolt.js")
	encoding = 'UTF-8'
}

minifyJs {
	source = combineJs
	dest = file("${buildDir}/web/revolt.js")
	closure {
		warningLevel = 'QUIET'
		compilerOptions = [
			languageIn: 'ECMASCRIPT6',
			languageOut: 'ECMASCRIPT5',
			rewritePolyfills: false
		]
	}
}

task combineUtilJs(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
	source = file("${projectDir}/src/js/util.buildorder").readLines().findAll { !it.trim().isEmpty() }
			.collect { "${projectDir}/src/js/${it}" }
	dest = file("${buildDir}/web/debug/revolt.utils.js")
	encoding = 'UTF-8'
}

task minifyUtilJs(type: com.eriwen.gradle.js.tasks.MinifyJsTask) {
	source = combineUtilJs
	dest = file("${buildDir}/web/revolt.utils.js")
}

task assembleRz(type: Zip, group: 'Build', dependsOn: [minifyCss, minifyFontsCss, minifyJs, minifyUtilJs]) {
	archiveName "${project.name}-rz.jar"
	destinationDir file("${buildDir}/libs/")
	from("${buildDir}") {
		include 'web/**'
		includeEmptyDirs = false
	}
	description "Assemble archive ${archiveName} into ${relativePath(destinationDir)}"
}

assemble.dependsOn assembleRz

artifacts {
	reszip assembleRz
}

publishing.publications {
	mavenRz(MavenPublication) {
		artifact source: assembleRz, extension: 'jar', classifier: 'rz'
	}
}
