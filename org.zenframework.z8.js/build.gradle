import org.gradle.api.tasks.InputDirectory

apply plugin: 'base'
apply plugin: 'maven'

configurations.default.attributes {
	attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, project.objects.named(LibraryElements, 'web'))
}

configurations {
	jstools
}

dependencies {
	jstools 'org.zenframework.z8.dependencies.minimizers:closure:3.0'
	jstools 'org.zenframework.z8.dependencies.minimizers:yuicompressor:3.0'
}

class ConcatTask extends DefaultTask {

	private static final String ENCODING = 'UTF-8'

	@InputDirectory final DirectoryProperty source = project.objects.directoryProperty()
	@OutputFile final RegularFileProperty output = project.objects.fileProperty()
	@Input def buildorder = '.buildorder'

	@TaskAction
	def run() {
		File source = this.source.asFile.get()
		project.logger.info "Concat from ${source.path}..."

		def src = project.file("${source.path}/${buildorder}").readLines().findAll {
			def path = it.trim()
			!path.isEmpty() && !path.startsWith('#')
		}.collect {
			project.file("${source.path}/${it}")
		}
		src.each { project.logger.info "Concat: ${it}" }
		def dest = output.asFile.get()
		dest.parentFile.mkdirs()
		dest.newWriter(ENCODING).withWriter { w ->
			src.each { f -> w << f.getText(ConcatTask.ENCODING) << '\n' }
		}
	}

}

task concatCss(type: ConcatTask) {
	source = file("${projectDir}/src/css")
	output = file("${buildDir}/web/css/revolt.css")
}

task concatFontsCss(type: ConcatTask) {
	source = file("${projectDir}/src/css")
	output = file("${buildDir}/web/css/fonts.css")
	buildorder = '.fonts.buildorder'
}

task concatJs(type: ConcatTask) {
	source = file("${projectDir}/src/js")
	output = file("${buildDir}/web/revolt.js")
}

task concatUtilJs(type: ConcatTask) {
	source = file("${projectDir}/src/js")
	output = file("${buildDir}/web/revolt.util.js")
	buildorder = '.util.buildorder'
}

task prepareResources(type: Copy) {
	from('src') {
		include 'css/fonts/**', 'css/img/**'
	}
	into "${buildDir}/web"
}

task assembleZip(type: Zip, group: 'Build', dependsOn: [concatCss, concatFontsCss, concatJs, concatUtilJs, prepareResources]) {
	archiveName "${project.name}-${project.version}.zip"
	destinationDir file("${buildDir}/libs/")
	from("${buildDir}") {
		include 'web/**'
		includeEmptyDirs = false
	}
	description "Assemble archive ${archiveName} into ${relativePath(destinationDir)}"
}

assemble.dependsOn assembleZip

artifacts.add('default', assembleZip) {
	builtBy assembleZip
}

publishing.publications {
	maven(MavenPublication) {
		artifact source: assembleZip, extension: 'zip'
	}
}
