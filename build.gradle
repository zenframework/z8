allprojects {
	group = 'org.zenframework.z8'
	version = '1.3.0'

	buildscript {
		repositories {
			mavenLocal()
			maven { url 'http://mvn.revoltsoft.ru/' }
			maven { url 'https://plugins.gradle.org/m2/' }
		}
	}

	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'http://mvn.revoltsoft.ru/' }
	}

	apply plugin: 'eclipse'
	apply plugin: 'maven-publish'

	ext.dependenciesVersion = '3.1'

	buildDir = "${projectDir}/target"

	publishing {
		repositories {
			mavenLocal()
		}
	}
}

subprojects {
	plugins.withType(JavaPlugin) {
		// Eclipse: default java output -> $buildDir/classes/default
		eclipse.classpath.defaultOutputDir = file("${buildDir}/classes/main")
		// Eclipse: java source folders output -> ${relativePath(buildDir)}/classes/...
		eclipse.classpath.file.whenMerged {
			entries.findAll { entry ->
				entry instanceof org.gradle.plugins.ide.eclipse.model.SourceFolder
			}.each { entry ->
				entry.output = entry.output.replace('bin/', "${relativePath(buildDir)}/classes/")
			}
		}

		sourceSets.main.java.outputDir = file("${buildDir}/classes/main")
		sourceSets.main.output.resourcesDir = file("${buildDir}/classes/main")

		jar.duplicatesStrategy = 'exclude'
		jar.includeEmptyDirs = false
	}

	// Java version 1.8
	tasks.withType(JavaCompile) {
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
		options.encoding = 'UTF-8'
	}
}

// Helper plugin for printing task tree (can be removed)
// Usage: gradle <task1> ... <taskN> taskTree
//buildscript {
//	dependencies {
//		classpath 'gradle.plugin.com.dorongold.plugins:task-tree:1.5'
//	}
//}
//
//apply plugin: 'com.dorongold.task-tree'
